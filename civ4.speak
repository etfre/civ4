priority() => 2
is_active() => true()
is_active() => 'civ iv: beyond the sword'
on_load() => extensions.register('civ4.civ4.py', 'civ4')
on_activate() => setState(grid, screengrid.Grid()) keyboard.add_delay(/numkey\d/, 1) civ4.start()
on_deactivate() => keyboard.remove_delay(/numkey\d/) print('civ 4 deactivate') civ4.stop()
mouseMove(pos) => mouse.move($pos[0], $pos[1])
moveUnit(key, count) => {$key}~$count
doubleClick() => mouse.click() wait(.05) mouse.click()
centerPos() => [300, 300]

literal_direction := up | down | right | left
edgeNorth := north=[600, 0]
edgeEast := east=[2000, 600]
edgeSouth := south=[600, 2000]
edgeWest := west=[0, 600]
edges := <edgeNorth> | <edgeEast> | <edgeSouth> | <edgeWest>
units := airship | archer | axeman | bomber | catapult | chariot | maceman | pikeman | rifleman | settler | spearman | swordsman | transport | trebuchet | warrior | work boat | worker
buildings := barracks | courthouse | forge | granary | library | university
national_wonders := palace | (national park)
world_wonders := (the pyramids) | stonehenge | shwedagon paya
buildable := <units> | <buildings> | <national_wonders> | <world_wonders>

build <buildable> = civ4.build($1)
<literal_direction> [<number>] = {$1}~$2
record macro <letter> = macro.record($1)
stop recording [<letter>] = macro.stop($1)
play <letter> = macro.play($1)
mouse grid = state.grid.overlay() 
grid select = state.grid.overlay(click=True) 
gray text = setattr(state.grid, 'font_color', [128,128,128]) 
center mouse = mouseMove(centerPos()) 
domestic advisor = {f1}
financial advisor = {f2}
change civics = {f3}
foreign advisor = {f4}
military advisor = {f5}
technology advisor | tech tree = {f6}
convert religion | religion advisor = {f7}
number <digit>_1- = directinput.send($1) 
north <digit>_0- = moveUnit(NumKey8, $1)
northeast <digit>_0- = moveUnit(NumKey9, $1) 
east <digit>_0- = moveUnit(NumKey6, $1) 
southeast <digit>_0- = moveUnit(NumKey3, $1) 
south <digit>_0- = moveUnit(NumKey2, $1) 
southwest <digit>_0- = moveUnit(NumKey1, $1) 
west <digit>_0- = moveUnit(NumKey4, $1) 
northwest <digit>_0- = moveUnit(NumKey7, $1) 
pan <edges> = mouseMove($1) 
zoom in [<number>] = (mouse.wheel(1) wait(.1))~$1
zoom out [<number>] = (mouse.wheel(-1) wait(.1))~$1
toggle interface = {alt i}
toggle world builder = {ctrl w}

select same = {ctrl g} {esc}
select all = {alt g} {esc}
move same = mouse.move(300, 300) {ctrl g} state.grid.overlay(click=True) 
move all = mouse.move(300, 300) {alt g} state.grid.overlay(click=True) 
go to = mouse.move(300, 300) g state.grid.overlay(click=True)
route to = mouse.move(300, 300) {alt r}  state.grid.overlay(click=True)

build [a] road = r
sleep | fortify = f
skip turn = {space}
build city = b