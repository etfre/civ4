priority() => 2
is_active() => true()
is_active() => 'civ iv: beyond the sword'
on_load() => extensions.register('civ4.civ4.py', 'civ4')
on_activate() => setState(grid, screengrid.Grid()) keyboard.add_delay(/numkey\d/, 1) civ4.start()
on_deactivate() => keyboard.remove_delay(/numkey\d/) print('civ 4 deactivate') civ4.stop()
mouseMove(pos) => mouse.move($pos[0], $pos[1])
moveUnit(key, count) => {$key}~$count
doubleClick() => mouse.click() wait(.05) mouse.click()
centerPos() => [300, 300]

literal_direction := up | down | right | left
edgeNorth := north=[600, 0]
edgeEast := east=[2000, 600]
edgeSouth := south=[600, 2000]
edgeWest := west=[0, 600]
edges := <edgeNorth> | <edgeEast> | <edgeSouth> | <edgeWest>
religion := buddhist | christian | confucian | jewish | hindu | islamic | taoist
armored_unit := modern armor | panzer | tank
air_units := airship | bomber | fighter | gunship | jet fighter | skirmisher | stealth bomber
archer_units := archer | bowman | (cho ko new)='cho-ko-nu' | crossbowman | longbowman
gunpowder_units := grenadier | infantry | machine gun | ((mech | mechanized)=mechanized) infantry | musketman | rifleman
melee_units := axeman | maceman | (catch you a)=quechua | pikeman | samurai | spearman | swordsman | warrior
mounted_unit := cavalry | chariot | horse archer | immortal | knight | war elephant
naval_unit := destroyer | galley | transport | trireme | work boat
siege_unit := artillery | catapult | cannon | mobile artillery | trebuchet
special_unit := <religion> missionary | scout | settler | spy | worker
unit := <air_units> | <archer_units> | <armored_unit> | <gunpowder_units> | <melee_units> | <mounted_unit> | <naval_unit> | <siege_unit> | <special_unit>
religious_building := <religion> (temple | monastary)
building := <religious_building> | barracks | courthouse | forge | granary | library | lighthouse | market | monument | theater | university | walls
national_wonder := palace | national park | maoi statues
world_wonder := (mausoleum of maw so law sis)='mausoleum of mausollus' | stonehenge | shwedagon paya | the great lighthouse | the pyramids 
other_builable := wealth | research | culture
buildable := <unit> | <building> | <national_wonder> | <world_wonder> | <other_builable>
improvement := farm=i | cottage=t | watermill={shift w} | road=r | workshop=k | fort={ctrl f} | mine=m | quarry=q
promotion := (combat 1)='combat i' | (combat 2)='combat ii' | (city raider 1)='city raider i' | (drill 1)='drill i' | (woodsman 1)='woodsman i'

promote <promotion> = civ4.click_asset('Promotions', $1)
create group | creates a group = civ4.click_asset('Unit Commands', 'creates a group')
separate group | separates the group = civ4.click_asset('Unit Commands', 'separates the group')
great scientist = civ4.click_asset('Unit Commands', 'great scientist')
discover technology = civ4.click_asset('Unit Commands', 'discover technology')
[start a] golden age = civ4.click_asset('Unit Commands', 'golden age')
build <buildable> = civ4.build($1)
examine city = civ4.click_asset('Start Turn Build', 'examine city')
<literal_direction> key [<number>] = {$1}~$2
record macro <letter> = macro.record($1)
stop recording [<letter>] = macro.stop($1)
play <letter> = macro.play($1)
mouse grid = state.grid.overlay() 
grid select = state.grid.overlay(click=True) 
gray text = setattr(state.grid, 'font_color', [128,128,128]) 
center mouse = mouseMove(centerPos()) 
domestic advisor = {f1}
financial advisor = {f2}
change civics = {f3}
foreign advisor = {f4}
military advisor = {f5}
technology advisor | tech tree = {f6}
convert religion | religion advisor = {f7}
number <digit>_1- = directinput.send($1) 
north <digit>_0- = moveUnit(NumKey8, $1)
northeast <digit>_0- = moveUnit(NumKey9, $1) 
east <digit>_0- = moveUnit(NumKey6, $1) 
southeast <digit>_0- = moveUnit(NumKey3, $1) 
south <digit>_0- = moveUnit(NumKey2, $1) 
southwest <digit>_0- = moveUnit(NumKey1, $1) 
west <digit>_0- = moveUnit(NumKey4, $1) 
northwest <digit>_0- = moveUnit(NumKey7, $1) 
pan <edges> = mouseMove($1) 
zoom in [<number>] = (mouse.wheel(1) wait(.1))~$1
zoom out [<number>] = (mouse.wheel(-1) wait(.1))~$1
toggle interface = {alt i}
toggle world builder = {ctrl w}
scroll (up | down) [<number>] = civ4.scroll($1, $2)

select same = {ctrl g} {esc}
select all = {alt g} {esc}
(focus | select) <unit> [<number>] = var(do_select, $1 == select) civ4.focus_unit($2, parse_int($3), none(), $do_select)
(focus | select) <unit> <number> through <number> = var(do_select, $1 == select) civ4.focus_unit($2, parse_int($3), parse_int($4), $do_select)
(focus | select) <unit> <number> through right <number> = var(do_select, $1 == select) civ4.focus_unit($2, parse_int($3), -parse_int($4), $do_select)
(focus | select) <unit> right <number> through <number> = var(do_select, $1 == select) civ4.focus_unit($2, -parse_int($3), -parse_int($4), $do_select)

move same = mouse.move(300, 300) {ctrl g} state.grid.overlay(click=True) 
move all = mouse.move(300, 300) {alt g} state.grid.overlay(click=True) 
go to [mode] = mouse.move(300, 300) g state.grid.overlay(click=True)
route to = mouse.move(300, 300) {alt r}  state.grid.overlay(click=True)

build [a | an] <improvement> = $2
sleep | fortify = f
wake up | wake [the] unit = f
skip turn = {space}
finish turn = {shift enter}
build city = b
sentry = s
build improvements = a
build trade network = n
improve nearest city = {shift ctrl c}
create fishing boats = f
(cancel | stop) automation = {backspace}
explore = e
fortify until healed = h
pillage = {shift p}
save game = {ctrl s}
load game = {ctrl l}
game <number> = civ4.select_saved_game(int($1))

click ok = civ4.click_first_matching_asset([[none(), 'ok button'], [none(), 'ok button 2']])
click cancel = civ4.click_first_matching_asset([[none(), 'cancel button'], [none(), 'cancel button 2']])

nearest city | city screen = {ins}
previous unit = '\\'
next unit = {enter}
previous city [<number>] = {home}~$1
next city [<number>] = {end}~$1

increase (research | science) [<number>] = civ4.click_at(80, 60)
reduce (research | science) [<number>] = civ4.click_at(100, 60)
increase culture [<number>] = civ4.click_at(80, 80)
reduce culture [<number>] = civ4.click_at(100, 80)
increase espionage [<number>] = civ4.click_at(80, 100)
reduce espionage [<number>] = civ4.click_at(100, 100)

whip production = civ4.click_at(-270, -95)
buy production = civ4.click_at(-235, -95)

automate production | production automation = civ4.click_at(-270, -70)
automate citizen | citizen automation = civ4.click_at(-235, -70)

emphasize food = civ4.click_at(-275, -45)
emphasize production = civ4.click_at(-255, -45)
emphasize commerce = civ4.click_at(-235, -45)

emphasize research = civ4.click_at(-275, -25)
emphasize great person = civ4.click_at(-255, -25)
avoid growth = civ4.click_at(-235, -25)
